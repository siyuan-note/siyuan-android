plugins {
    id 'com.android.application'
}

// By appending the version name, build type, and a timestamp to the APK filename,
// it becomes very easy for testers and developers to identify exactly which version of the application they are testing.
// This is particularly useful when multiple versions or builds are being tested simultaneously.
// Testers don’t need to manually rename APKs to keep track of different builds or worry about which version is the latest.
android.applicationVariants.configureEach { variant ->
    variant.outputs.configureEach { output ->
        def formattedDate = new Date().format('yyyyMMddHHmmss')
        def versionName = variant.versionName
        def buildType = variant.buildType.name
        def newApkName = "siyuan-${versionName}-${buildType}-${formattedDate}.apk"
        output.outputFileName = newApkName
    }
}

android {
    compileSdkVersion 34
    buildToolsVersion "34"

    defaultConfig {
        applicationId "org.b3log.siyuan"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 229
        versionName "3.0.9"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            resValue "string", "app_name", "SiYuan"
            resValue "string", "app_package_name", "org.b3log.siyuan"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            resValue "string", "app_name", "SiYuan-Debug"
            resValue "string", "app_package_name", "org.b3log.siyuan.debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES',
                         'META-INF/LICENSE',
                         'META-INF/LICENSE.txt',
                         'META-INF/license.txt',
                         'META-INF/NOTICE',
                         'META-INF/NOTICE.txt',
                         'META-INF/notice.txt',
                         'plugin.properties']
        }
        jniLibs {
            useLegacyPackaging true
        }
    }

    lint {
        abortOnError false
    }
    namespace 'org.b3log.siyuan'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.lifecycle:lifecycle-process:2.6.1'
    implementation 'androidx.work:work-runtime:2.8.1'

    implementation(name: 'kernel', ext: 'aar')
    implementation("commons-io:commons-io:2.5") // 不要升级，否则无法兼容 Android 7
    implementation 'com.gitee.zackratos:UltimateBarX:0.8.0'
    implementation 'com.blankj:utilcodex:1.31.1'
    implementation 'com.koushikdutta.async:androidasync:2.2.1'
}
